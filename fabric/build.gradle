plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mod_id", project.mod_id

    filesMatching("fabric.mod.json") {
        expand([
            "version": project.version,
            "mod_id": project.mod_id,
            "mod_name": project.mod_name,
            "description": project.description,
            "mod_author": project.mod_author,
            "license": project.license,
            "fabric_loader_version": project.fabric_loader_version,
            "minecraft_version": project.minecraft_version,
            "java_version": project.java_version,
            "group": project.group,

            "website_link": project.website_link,
            "issues_link": project.issues_link,
            "source_link": project.source_link,
        ])
    }

    filesMatching("slice.mixins.json") {
        expand([
            "group": project.group,
        ])
    }
}